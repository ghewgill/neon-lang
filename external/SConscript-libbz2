import os
import shutil
import sys
import tarfile

Import("env")

libbz2 = None
include_bz2 = None

bz2env = Environment()
if not env["RELEASE"]:
    if sys.platform == "win32":
        bz2env.Append(CFLAGS=["/MTd"])

if GetOption("clean"):
    shutil.rmtree("bzip2-1.0.6", ignore_errors=True)
elif not os.path.exists("bzip2-1.0.6/Makefile"):
    tarfile.open("bzip2-1.0.6.tar.gz").extractall(".")

if sys.platform == "win32":
    libbz2 = bz2env.Library("bzip2-1.0.6/libbz2.lib", [
        "bzip2-1.0.6/blocksort.c",
        "bzip2-1.0.6/bzlib.c",
        "bzip2-1.0.6/compress.c",
        "bzip2-1.0.6/crctable.c",
        "bzip2-1.0.6/decompress.c",
        "bzip2-1.0.6/huffman.c",
        "bzip2-1.0.6/randtable.c",
    ])
    include_bz2 = "external/bzip2-1.0.6"
else:
    conf = Configure(env)
    if not conf.CheckLibWithHeader("bz2", "bzlib.h", "c"):
        libbz2 = bz2env.Command("bzip2-1.0.6/libbz2.a", "bzip2-1.0.6/Makefile", "cd external/bzip2-1.0.6 && make")
        include_bz2 = "external/bzip2-1.0.6"
    conf.Finish()

Return(["libbz2", "include_bz2"])
