import os
import sys
import tarfile

def find_tool(name):
    justname = os.path.basename(name)
    for p in env["ENV"]["PATH"].split(";"):
        fn = os.path.join(p, justname)
        if os.access(fn, os.F_OK):
            return fn
        fn = os.path.join(p, name)
        if os.access(fn, os.F_OK):
            return fn
    print >>sys.stderr, "could not find tool:", name
    sys.exit(1)

def subs(target, source, env):
    with open(target[0].path, "w") as outf:
        with open(source[0].path) as inf:
            outf.write(inf.read()
                .replace("@VERSION@", "3.2.1")
                .replace("@TARGET@", "X86_WIN64")
                .replace("@HAVE_LONG_DOUBLE@", "HAVE_LONG_DOUBLE")
                .replace("@HAVE_LONG_DOUBLE_VARIANT@", "HAVE_LONG_DOUBLE_VARIANT")
                .replace("@FFI_EXEC_TRAMPOLINE_TABLE@", "FFI_EXEC_TRAMPOLINE_TABLE")
            )

def fix_target(target, source, env):
    with open(target[0].path, "w") as outf:
        with open(source[0].path) as inf:
            for s in inf:
                if s.startswith("#define FFI_TARGET_HAS_COMPLEX_TYPE"):
                    s = "//" + s
                outf.write(s)

def sub_config(target, source, env):
    with open(target[0].path, "w") as outf:
        with open(source[0].path) as inf:
            for s in inf:
                if s.startswith("#undef"):
                    if s.split()[1] in [
                        "HAVE_MEMCPY",
                        "FFI_NO_RAW_API",
                    ]:
                        s = "#define {} 1".format(s.split()[1])
                outf.write(s)

def remove_short(target, source, env):
    with open(target[0].path, "w") as outf:
        with open(source[0].path) as inf:
            outf.write(inf.read().replace("SHORT", ""))

Import("env")

if not os.path.exists("external/libffi-3.2.1/configure"):
    tarfile.open("external/libffi-3.2.1.tar.gz").extractall("external")
if sys.platform == "win32":
    env.Append(CPPDEFINES=["FFI_BUILDING"])
    ffienv = env.Clone()
    ffienv.Append(CPPPATH=["external/libffi-3.2.1/x86-win64/include"])
    ffi_h = ffienv.Command("external/libffi-3.2.1/x86-win64/include/ffi.h", "external/libffi-3.2.1/include/ffi.h.in", subs)
    ffitarget_h = ffienv.Command("external/libffi-3.2.1/x86-win64/include/ffitarget.h", "external/libffi-3.2.1/src/x86/ffitarget.h", fix_target)
    fficommon_h = ffienv.Command("external/libffi-3.2.1/x86-win64/include/ffi_common.h", "external/libffi-3.2.1/include/ffi_common.h", Copy("$TARGET", "$SOURCE"))
    fficonfig_h = ffienv.Command("external/libffi-3.2.1/x86-win64/include/fficonfig.h", "external/libffi-3.2.1/fficonfig.h.in", sub_config)
    win64_p = ffienv.Command("external/libffi-3.2.1/x86-win64/win64.p", "external/libffi-3.2.1/src/x86/win64.S", "cl /EP /I external/libffi-3.2.1/x86-win64/include $SOURCE >$TARGET")
    win64_asm = ffienv.Command("external/libffi-3.2.1/x86-win64/win62.asm", win64_p, remove_short)
    objects = [
        ffienv.Object("external/libffi-3.2.1/x86-win64/closures.obj", "external/libffi-3.2.1/src/closures.c"),
        ffienv.Object("external/libffi-3.2.1/x86-win64/ffi.obj", "external/libffi-3.2.1/src/x86/ffi.c"),
        ffienv.Object("external/libffi-3.2.1/x86-win64/prep_cif.obj", "external/libffi-3.2.1/src/prep_cif.c"),
        ffienv.Object("external/libffi-3.2.1/x86-win64/types.obj", "external/libffi-3.2.1/src/types.c"),
    ]
    for o in objects:
        ffienv.Depends(o, [ffi_h, ffitarget_h, fficommon_h, fficonfig_h])
    libffi = ffienv.Library("external/libffi-3.2.1/x86-win64/libffi.lib", objects + [
        ffienv.Command("external/libffi-3.2.1/x86-win64/win64.obj", win64_asm, "\"{}\" /c /Cx /Fo$TARGET $SOURCE".format(find_tool("x86_amd64/ml64.exe"))),
    ])
    ffienv.Install("external/lib/libffi-3.2.1/include/", [ffi_h, ffitarget_h, fficommon_h, fficonfig_h])
    libffi = ffienv.Install("external/lib/", libffi)
else:
    env.Command("external/libffi-3.2.1/Makefile", "external/libffi-3.2.1/configure", "cd external/libffi-3.2.1 && ./configure --prefix=`pwd`/..")
    libffi = env.Command("external/lib/libffi.a", "external/libffi-3.2.1/Makefile", "cd external/libffi-3.2.1 && make && make install")

env.Append(CPPPATH=["external/lib/libffi-3.2.1/include"])

Return(["libffi"])
